plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}

def versionPropertiesFile = rootProject.file("./version.properties")
def versionProperties = new Properties()
versionProperties.load(new FileInputStream(versionPropertiesFile))

def apiConfigsPropertiesFile = rootProject.file("./apiconfigs.properties")
def apiConfigsProperties = new Properties()
apiConfigsProperties.load(new FileInputStream(apiConfigsPropertiesFile))

android {
    namespace 'co.nimblehq.loyalty.sdk'
    compileSdk 33

    defaultConfig {
        minSdk 24
        targetSdk 33
        versionCode 1
        versionName versionProperties['versionName']

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        buildConfigField "String", "BASE_API_URL", apiConfigsProperties["baseApiUrl"]
        buildConfigField "String", "AUTHENTICATION_API_URL", apiConfigsProperties["authenticationApiUrl"]
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.8.0'

    implementation 'androidx.security:security-crypto-ktx:1.1.0-alpha05'
    implementation "androidx.security:security-identity-credential:1.0.0-alpha03"

    implementation 'com.github.nimblehq:android-common-ktx:0.1.1'

    api 'com.squareup.retrofit2:retrofit:2.9.0'
    api 'com.squareup.okhttp3:okhttp:4.10.0'
    api 'com.squareup.moshi:moshi:1.12.0'
    api 'com.squareup.retrofit2:converter-moshi:2.9.0'
    api 'com.squareup.moshi:moshi-adapters:1.12.0'
    api 'com.squareup.moshi:moshi-kotlin:1.12.0'
    api 'com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.10'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                // You can then customize attributes of the publication as shown below.
                groupId = 'co.nimblehq'
                artifactId = 'loyalty.sdk'
                version = versionProperties['versionName']
            }
        }
    }
}
